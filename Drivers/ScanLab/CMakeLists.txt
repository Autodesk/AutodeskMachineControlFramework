#[[++

Copyright (C) 2020 Autodesk Inc.

All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:
	* Redistributions of source code must retain the above copyright
	  notice, this list of conditions and the following disclaimer.
	* Redistributions in binary form must reproduce the above copyright
	  notice, this list of conditions and the following disclaimer in the
	  documentation and/or other materials provided with the distribution.
	* Neither the name of the Autodesk Inc. nor the
	  names of its contributors may be used to endorse or promote products
	  derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
DISCLAIMED. IN NO EVENT SHALL AUTODESK INC. BE LIABLE FOR ANY
DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
(INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
(INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

]]


cmake_minimum_required(VERSION 3.5)

##########################################################################################
### Change the next line for making a new driver
##########################################################################################

set (DRIVERPROJECT ScanLab)

##########################################################################################
### generic CMake boilerplate code
##########################################################################################

project(LibMCDriver_${DRIVERPROJECT})

string(TOLOWER ${DRIVERPROJECT} DRIVERSUFFIX)
string(TOUPPER ${DRIVERPROJECT} DRIVERSUFFIX_UPPER)

set (DRIVERNAME libmcdriver_${DRIVERSUFFIX})

set (EXPORTFLAG __LIBMCDRIVER_${DRIVERSUFFIX_UPPER}_EXPORTS)

set (CMAKE_CXX_STANDARD 11)

# The location of autogenerated interfaces
set(CMAKE_CURRENT_HEADER_DIR ${CMAKE_CURRENT_SOURCE_DIR}/../../Framework/HeadersDev/CppDynamic)
set(CMAKE_CURRENT_AUTOGENERATED_DIR ${CMAKE_CURRENT_SOURCE_DIR}/../../Framework/InterfacesDev)
set(CMAKE_CURRENT_OUTPUT_DIR ${PROJECT_BINARY_DIR}/../../Output)

file(STRINGS ${PROJECT_BINARY_DIR}/../../githash.txt GLOBALGITHASH)
message(STATUS "Global git hash: \"${GLOBALGITHASH}\"")


file(GLOB LIBMCDRIVER_SRC
	${CMAKE_CURRENT_SOURCE_DIR}/Interfaces/*.cpp
	${CMAKE_CURRENT_SOURCE_DIR}/Implementation/*.cpp
	${CMAKE_CURRENT_SOURCE_DIR}/Implementation/serial/*.cc
)

add_library(${DRIVERNAME} SHARED ${LIBMCDRIVER_SRC})
# Do not prefix the binary's name with "lib" on Unix systems:
set_target_properties(${DRIVERNAME} PROPERTIES PREFIX "" IMPORT_PREFIX "" )
# The following two properties are crucial to reduce the number of undesirably exported symbols
set_target_properties(${DRIVERNAME} PROPERTIES CXX_VISIBILITY_PRESET hidden)
set_target_properties(${DRIVERNAME} PROPERTIES VISIBILITY_INLINES_HIDDEN ON)
# This makes sure symbols are exported
target_compile_options(${DRIVERNAME} PRIVATE "-D${EXPORTFLAG}")
target_compile_options(${DRIVERNAME} PRIVATE "-D__LIBMCDRIVER_EXPORTS")
target_include_directories(${DRIVERNAME} PRIVATE ${CMAKE_CURRENT_AUTOGENERATED_DIR})
target_include_directories(${DRIVERNAME} PRIVATE ${CMAKE_CURRENT_HEADER_DIR})
target_include_directories(${DRIVERNAME} PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/Implementation)
target_include_directories(${DRIVERNAME} PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/Interfaces)

set_target_properties(${DRIVERNAME}
    PROPERTIES
    RUNTIME_OUTPUT_DIRECTORY "${CMAKE_CURRENT_OUTPUT_DIR}"
    RUNTIME_OUTPUT_DIRECTORY_DEBUG "${CMAKE_CURRENT_OUTPUT_DIR}"
    RUNTIME_OUTPUT_DIRECTORY_RELEASE "${CMAKE_CURRENT_OUTPUT_DIR}"
	LIBRARY_OUTPUT_DIRECTORY "${CMAKE_CURRENT_OUTPUT_DIR}"
	ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_CURRENT_OUTPUT_DIR}"
	VS_DEBUGGER_COMMAND "${CMAKE_CURRENT_OUTPUT_DIR}/amc_server.exe"
	VS_DEBUGGER_WORKING_DIRECTORY "${CMAKE_CURRENT_OUTPUT_DIR}"	
	OUTPUT_NAME "${GLOBALGITHASH}_driver_${DRIVERSUFFIX}"	
)


##########################################################################################
### Copy driver header files
##########################################################################################

file (GLOB headerfiles LIST_DIRECTORIES false "${CMAKE_CURRENT_SOURCE_DIR}/Headers/CppDynamic/*.h*")
foreach(headerfile ${headerfiles})

	get_filename_component(headerfilename ${headerfile} NAME)
	
	add_custom_command(
        TARGET ${DRIVERNAME} POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E echo "copying ${headerfile}...")

	add_custom_command(
        TARGET ${DRIVERNAME} POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy
                ${headerfile}
                ${CMAKE_CURRENT_BINARY_DIR}/../../DevPackage/Framework/HeadersDriver/CppDynamic/${headerfilename})
				
				
endforeach()	

add_custom_command(
	TARGET ${DRIVERNAME} POST_BUILD
	COMMAND ${CMAKE_COMMAND} -E echo "building driver resources")

if(WIN32)
add_custom_command(
	TARGET ${DRIVERNAME} POST_BUILD
	COMMAND ${CMAKE_CURRENT_BINARY_DIR}/../../DevPackage/Framework/buildresources.exe
			${CMAKE_CURRENT_SOURCE_DIR}/Resources
			${PROJECT_BINARY_DIR}/../../Output/${GLOBALGITHASH}_driver_${DRIVERSUFFIX}.data)
endif()

if(UNIX)
add_custom_command(
	TARGET ${DRIVERNAME} POST_BUILD
	COMMAND ${CMAKE_CURRENT_BINARY_DIR}/../../DevPackage/Framework/buildresources.linux
			${CMAKE_CURRENT_SOURCE_DIR}/Resources
			${PROJECT_BINARY_DIR}/../../Output/${GLOBALGITHASH}_driver_${DRIVERSUFFIX}.data)
endif()


##########################################################################################
### Add custom code below
##########################################################################################



